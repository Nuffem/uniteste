lista # https://raw.githubusercontent.com/Nuffem/lista.0/0a20c969c400d2d09904f92b2082783da8e8c32b/lista.0

uniteste = {
  principal: testes => (
    principal2 = testes2 =>
      testes2[.] == 0 ? [0 "" 0] :
      testes2[.] == 1 ? [testes2[0][0] lista.juntar(testes2[0][1] 10) testes2[0][1][.]] :
      (
        resultado1 = principal2([testes2[0]])
        resultado2 = principal2(testes2[1:])
        [
          resultado1[0] + resultado2[0]
          lista.juntar([
            resultado1[1]
            resultado2[1]
          ] 10)
          resultado1[2] + resultado2[2]
        ]
      )
    resultado = principal2(testes)
    [resultado[0] lista.juntar([
      resultado[1]
      `Total de testes: ${"" + resultado[2]}`
      `Falharam: ${"" + resultado[0]}`
    ] 10)]
  )
  descrever: título testes =>
    testes[.] == 0 ? [0 []] :
    testes[.] == 1 ? [testes[0][0] lista.mapear(testes[0][1] mensagem => `${título} | ${mensagem}`)] :
    testes[.] == 2 ? uniteste.descrever(título [
      [
        testes[0][0] + testes[1][0]
        [
          ...testes[0][1]
          ...testes[1][1]
        ]
      ]
    ]) :
    (
      resultado1 = uniteste.descrever(título testes[0:2])
      resultado2 = uniteste.descrever(título testes[2:])
      [
        resultado1[0] + resultado2[0]
        [
          ...resultado1[1]
          ...resultado2[1]
        ]
      ]
    )
  iguais: valor1 valor2 => valor1 == valor2 ? [0 ["ok"]] : [1 [`Esperava que ${"" + valor1} fosse igual a ${"" + valor2}.`]]
  listas_iguais: lista1 lista2 =>
    lista1[.] != lista2[.] ? [1 ["Esperava listas iguais."]] :
    lista1[0] != lista2[0] ? [1 ["Esperava listas iguais."]] :
    lista1[.] > 1 ? uniteste.listas_iguais(lista1[1:] lista2[1:]) :
    [0 ["ok"]]
  objetos_iguais: objeto1 objeto2 =>
    objeto1[*][.] != objeto2[*][.] ? [1 ["Esperava objetos iguais."]] :
    objeto1[*][.] == 0 ? [0 ["ok"]] : (
      objetos_iguais2 = atributos1 atributos2 =>
        atributos1[0] != atributos2[0] ? [1 ["Esperava objetos iguais."]] :
        objeto1[atributos1[0]] != objeto2[atributos2[0]] ? [1 ["Esperava objetos iguais."]] :
        atributos1[.] > 1 ? objetos_iguais2(atributos1[1:] atributos2[1:]) :
        [0 ["ok"]]
      objetos_iguais2(objeto1[*] objeto2[*])
    )
}

uniteste